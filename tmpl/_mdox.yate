module "mdox"

external scalar mdox-string-trim(scalar)
external boolean mdox-string-empty(scalar)
external boolean mdox-array-inarray(scalar, array)

match .* mdox-tag-types (array tags) {
    apply .tags[mdox-array-inarray(.type, tags)] mdox-tag-types-str
}

match .* mdox-tag-types-str {
    str = (
        for (.types) {
            if (index() != 0) {
                "|"
            }
            .
        }
    )

    if (!mdox-string-empty(str)) {
        "`"
        str
        "`"
    }
}


match .* tags-flags (array tags) {
    apply .tags[mdox-array-inarray(.type, tags)] tags-flags-str
}

match .* tags-flags-str {
    "`{.type}` "
}


match .* description {
    tags-type = [
        'summary'
        'classdesc'
        'desc'
        'description'
    ]

    description = (
        for (.tags) {
            if (mdox-array-inarray(.type, tags-type) && .description) {
                .description
                "\n\n"
            }
        }

        if (.description.full) {
            .description.full
            "\n\n"
        }
    )

    mdox-string-trim(description)
}


match .* example {
    example = (
        for (.tags[.type == 'example']) {
            if (index() != 0) {
                "\n\n"
            }
            .title
            "\n"
            "```js\n"
            .description
            "\n```"
        }
    )

    if (!mdox-string-empty(example)) {
        "\n\n"
        "##### **Example**"
        "\n\n"
        example
    }
}


match .* access {
    access = (
        if (.tags[.type == 'access'].access) {
            .tags[.type == 'access'].access

        } else if (.tags[.type == 'private']) {
            "private"

        } else if (.tags[.type == 'protected']) {
            "protected"

        } else if (.tags[.type == 'public']) {
            "public"
        }
    )

    if (!mdox-string-empty(access)) {
        "`{access}` "
    }
}


match .* argument-description (array tags-type) {
    for (.tags[mdox-array-inarray(.type, tags-type)]) {
        if (index() != 0) {
            "\n"
        }

        " - "

        if (.optional) {
            "["
        }

        .name

        " "
        apply . mdox-tag-types-str

        if (.nonNullable) {
            " `non nullable`"
        } else if (.nullable) {
            " `nullable`"
        }

        if (.optional) {
            "]"
        }

        if (.default) {
            " = ``"
            .default
            "``"
        }

        if (.description) {
            " : "
            .description
        }
    }
}


match .* type-description (array tags-type) {
    for (.tags[mdox-array-inarray(.type, tags-type)]) {
        if (index() != 0) {
            "\n"
        }

        " - "

        apply . mdox-tag-types-str

        if (.description) {
            " : "
            .description
        }
    }
}
